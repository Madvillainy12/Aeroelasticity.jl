<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Examples · AerostructuralDynamics.jl</title><script data-outdated-warner src="../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.039/juliamono-regular.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.11/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL=".."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../assets/documenter.js"></script><script src="../siteinfo.js"></script><script src="../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../">AerostructuralDynamics.jl</a></span></div><form class="docs-search" action="../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../">Home</a></li><li class="is-active"><a class="tocitem" href>Examples</a><ul class="internal"><li><a class="tocitem" href="#Two-Dimensional-Aeroelastic-Analysis"><span>Two-Dimensional Aeroelastic Analysis</span></a></li><li><a class="tocitem" href="#Three-Dimensional-Aeroelastic-Analysis"><span>Three Dimensional Aeroelastic Analysis</span></a></li></ul></li><li><a class="tocitem" href="../theory/">Theory</a></li><li><a class="tocitem" href="../developer/">Developer Guide</a></li><li><a class="tocitem" href="../library/">Library</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li class="is-active"><a href>Examples</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Examples</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/byuflowlab/AerostructuralDynamics.jl/blob/master/docs/src/examples.md" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Examples"><a class="docs-heading-anchor" href="#Examples">Examples</a><a id="Examples-1"></a><a class="docs-heading-anchor-permalink" href="#Examples" title="Permalink"></a></h1><h2 id="Two-Dimensional-Aeroelastic-Analysis"><a class="docs-heading-anchor" href="#Two-Dimensional-Aeroelastic-Analysis">Two-Dimensional Aeroelastic Analysis</a><a id="Two-Dimensional-Aeroelastic-Analysis-1"></a><a class="docs-heading-anchor-permalink" href="#Two-Dimensional-Aeroelastic-Analysis" title="Permalink"></a></h2><p>In this example, we demonstrate how to perform a two-dimensional aeroelastic analysis using a typical section model with two degrees of freedom.</p><p><img src="../typical-section.svg" alt/></p><p>The equations of motion for this model are</p><p class="math-container">\[m \left(\ddot{h}+b x_\theta \ddot{\theta} \right) + k_h h = -L \\
I_P \ddot{\theta} + m b x_\theta \ddot{h} + k_\theta = M\]</p><p>where <span>$b$</span> is the semichord length, <span>$k_h$</span> is the linear spring constant, <span>$k_\theta$</span> is the torsional spring constant, <span>$m$</span> is the mass per unit span, <span>$x_\theta$</span> is the distance to the center of mass from the reference point, <span>$I_θ$</span> is the moment of inertia about the reference point, <span>$L$</span> is the lift per unit span, and <span>$M$</span> is the moment per unit span about the reference point.</p><p>We use the non-dimensional parameters</p><p class="math-container">\[a = -1/5 \quad e = -1/10  \\
r^2 = \frac{I_P}{m b^2} \quad \sigma = \frac{\omega_h}{\omega_\theta} \\
\mu = \frac{m}{\rho_\infty \pi b^2} \quad V = \frac{U}{b \omega_\theta}\]</p><p>where <span>$a$</span> is the normalized distance from the semichord to the reference point, <span>$e$</span> is the normalized distance from the semichord to the center of mass, and <span>$\omega_h$</span> and <span>$\omega_\theta$</span> are the uncoupled natural frequencies.</p><p class="math-container">\[\omega_h = \sqrt{\frac{k_h}{m}} \quad \omega_\theta = \sqrt{\frac{k_\theta}{I_P}}\]</p><p>We perform aeroelastic analyses using a variety of aerodynamic models in order to compare the various models.</p><pre><code class="language-julia">using AerostructuralDynamics, LinearAlgebra

# reduced velocity range
V = range(0, 3.1, length=5000) # (reduced velocity)

# non-dimensional parameters
a = -1/5 # reference point normalized location
e = -1/10 # center of mass normalized location
μ = 20 # = m/(ρ*pi*b^2) (mass ratio)
r2 = 6/25 # = Iθ/(m*b^2) (radius of gyration about P)
σ = 2/5 # = ωh/ωθ (natural frequency ratio)
xθ = e - a
a0 = 2*pi # lift curve slope
α0 = 0 # zero lift angle

# chosen dimensional parameters
b = 1
ρ = 1
ωθ = 1

# derived dimensional parameters
m = μ*ρ*pi*b^2
Sθ = m*xθ*b
Iθ = r2*m*b^2
ωh = σ*ωθ
kh = m*ωh^2
kθ = Iθ*ωθ^2

# dimensionalized velocity
U = V*b*ωθ

# aerodynamic models
aerodynamic_models = (Steady(), QuasiSteady(), Wagner(), Peters{6}())

# structural model
structural_model = TypicalSection()

# eigenvalue storage
λ = Vector{Matrix{ComplexF64}}(undef, length(aerodynamic_models))

# loop through each aerodynamic model
for (ia, aerodynamic_model) in enumerate(aerodynamic_models)

    # combined models
    models = (aerodynamic_model, structural_model)

    # eigenvalue storage
    λ[ia] = zeros(ComplexF64, number_of_states(models), length(V))

    # loop through each reduced frequency
    for i = 1:length(V)
        # state variables
        u_aero = zeros(number_of_states(aerodynamic_model))
        u_stru = zeros(number_of_states(structural_model))
        u = vcat(u_aero, u_stru)

        # parameters
        p_aero = [a, b, a0, α0]
        p_stru = [kh, kθ, m, Sθ, Iθ]
        p_input = [U[i], ρ]
        p = vcat(p_aero, p_stru, p_input)

        # time
        t = 0.0

        # calculate inputs
        y = get_inputs(models, u, p, t)

        # mass matrix
        M = get_mass_matrix(models, u, y, p, t)

        # jacobian
        J = get_state_jacobian(models, u, y, p, t)

        # solve generalized eigenvalue problem
        λ[ia][:,i] = sort(eigvals(J, M), by=LinearAlgebra.eigsortby)
    end
end</code></pre><p>We now plot the results predicted using each aerodynamic model.</p><pre><code class="language-julia">using Plots
pyplot()

default(
    titlefontsize = 14,
    legendfontsize = 11,
    guidefontsize = 14,
    tickfontsize = 11,
    foreground_color_legend = nothing,
    background_color_legend = nothing,
    minorgrid=true,
    framestyle = :zerolines)

sp1 = plot(
    title = &quot;Non-Dimensional Frequency&quot;,
    xlim = (0,3.1),
    xtick = 0.0:0.5:3.0,
    xlabel = &quot;\$ \\frac{U}{b \\omega_\\theta} \$&quot;,
    ylim = (0, 1.05),
    ytick = 0.0:0.2:1.0,
    ylabel = &quot;\$ \\frac{\\Omega}{\\omega_\\theta} \$&quot;,
    legend = :topright
    )

sp2 = plot(
    title = &quot;Non-Dimensional Damping&quot;,
    xlim = (0,3.1),
    xtick = 0.0:0.5:3.0,
    xlabel = &quot;\$ \\frac{U}{b \\omega_\\theta} \$&quot;,
    ylim = (-0.7, 0.605),
    ytick = -0.6:0.2:0.6,
    ylabel = &quot;\$ \\frac{Γ}{\\omega_\\theta} \$&quot;,
    legend = :topleft
    )

labels = [&quot;Steady&quot;, &quot;Quasi-Steady&quot;, &quot;Wagner&quot;, &quot;Peters (N=6)&quot;]

for ia = 1:length(aerodynamic_models)

    scatter!(sp1, V, imag.(λ[ia][1,:])/ωθ,
        label = labels[ia],
        color = ia,
        markersize = 1,
        markerstrokewidth = 0,
        )

    for i = 2:size(λ[ia], 1)
        scatter!(sp1, V, imag.(λ[ia][i,:])/ωθ,
            label = &quot;&quot;,
            color = ia,
            markersize = 1,
            markerstrokewidth = 0,
            )
    end

    scatter!(sp2, V, real.(λ[ia][1,:])/ωθ,
        label = labels[ia],
        color = ia,
        markersize = 1,
        markerstrokewidth = 0,
        )

    for i = 2:size(λ[ia], 1)
        scatter!(sp2, V, real.(λ[ia][i,:])/ωθ,
            label = &quot;&quot;,
            color = ia,
            markersize = 1,
            markerstrokewidth = 0,
            )
    end
end

p1 = plot(sp1, sp2, layout = (2, 1), size = (600, 800))</code></pre><p><img src="../two-dimensional-stability.svg" alt/></p><p>The same analysis and results are presented by Hodges and Pierce in &quot;Introduction to Structural Dynamics and Aeroelasticity&quot; for the steady state and Peters&#39; Finite State aerodynamic models.  The results shown here match with those provided by Hodges and Pierce, thus validating our implementation of these models.</p><h2 id="Three-Dimensional-Aeroelastic-Analysis"><a class="docs-heading-anchor" href="#Three-Dimensional-Aeroelastic-Analysis">Three Dimensional Aeroelastic Analysis</a><a id="Three-Dimensional-Aeroelastic-Analysis-1"></a><a class="docs-heading-anchor-permalink" href="#Three-Dimensional-Aeroelastic-Analysis" title="Permalink"></a></h2><p>In this example, we demonstrate how to perform a three-dimensional aeroelastic analysis using geometrically exact beam theory in combination with various aerodynamic models.</p><pre><code class="language-">using AerostructuralDynamics, GXBeam, NLsolve, LinearAlgebra

# discretization
N = 8 # number of elements

# geometric properties
span = 16 # m
chord = 1 # m (chord)
xref = 0.5 # normalized reference location (from leading edge)
xcg = 0.5 # center of gravity (from leading edge)

# freestream properties
Vinf = range(1, 30, length=50) # m/s (velocity)
α = 2*pi/180 # angle of attack

# aerodynamic section properties
a = xref - 0.5 # normalized reference location (relative to semi-chord)
b = chord / 2 # m (semi-chord)
ρ = 0.088 # kg/m^3 (air density)
a0 = 2*pi # lift slope (for each section)
α0 = 0 # zero lift angle of attack (for each section)

# structural section properties
EIcc = 2e4 # N*m^2 (flat bending rigidity)
EInn = 4e6 # N*m^2 (chord bending rigidity)
GJ = 1e4 # N*m^2 (torsional rigidity)
μ = 0.75 # kg/m (mass per unit span)
i11 = 0.1 # kg*m (rotational inertia per unit span)
i22 = 0.0375 # moment of inertia about beam y-axis
i33 = 0.0625 # moment of inertia about beam z-axis

# define geometry
xpt = range(0, 0, length=N+1) # point x-coordinates (in body frame)
ypt = range(0, span, length=N+1) # point y-coordinates (in body frame)
zpt = range(0, 0, length=N+1) # point z-coordinates (in body frame)
points = [[xpt[i],ypt[i],zpt[i]] for i = 1:N+1]
start = 1:N # starting point of each beam element
stop = 2:N+1 # ending point of each beam element
frames = fill([0 1 0; 1 0 0; 0 0 -1], N) # local to body frame transformation
compliance = fill(Diagonal([0, 0, 0, 1/GJ, 1/EIcc, 1/EInn]), N) # compliance matrix
mass = fill(Diagonal([μ, μ, μ, i11, i22, i33]), N) # mass matrix
assembly = GXBeam.Assembly(points, start, stop; frames, compliance, mass)

# boundary condition initialization
prescribed = Dict(
    # fixed left edge
    1 =&gt; GXBeam.PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0,
        theta_z=0),
)

# distributed load initialization
distributed = Dict()
for i = 1:N
    # distributed load on each beam element
    distributed[i] = GXBeam.DistributedLoads(assembly, i)
end

# structural system initialization
system = GXBeam.System(assembly, keys(prescribed), false)

# construct aerodynamic model
aerodynamic_model = LiftingLine{N}(Peters{6}())

# construct structural model
structural_model = GEBT(system, assembly, prescribed, distributed)

# define simulation models
models = (aerodynamic_model, structural_model)

# eigenvalue storage
λ = zeros(ComplexF64, number_of_states(models), length(Vinf))

# state variable initial guess
u0 = zeros(number_of_states(models))

# loop through each velocity
for i = 1:length(Vinf)

    println(&quot;Vinf: &quot;, Vinf[i])

    # set parameters and current time
    p_aero = vcat(fill([a, b, a0, α0], N)...)
    p_stru = Float64[]
    p_additional = [-Vinf[i]*cos(α), 0, -Vinf[i]*sin(α), ρ]
    p = vcat(p_aero, p_stru, p_additional)
    t = 0

    # find state variables corresponding to steady state operating conditions
    fresid = u -&gt; get_rates(models, u, get_inputs(models, u, p, t), p, t)
    sol = nlsolve(fresid, u0)
    u = sol.zero

    # calculate the inputs corresponding to steady state operating conditions
    y = get_inputs(models, u, p, t)

    # calculate the mass matrix corresponding to steady state operating conditions
    M = get_mass_matrix(models, u, y, p, t)

    # calculate the jacobian corresponding to steady state operating conditions
    J = get_state_jacobian(models, u, y, p, t)

    # solve the generalized eigenvalue problem
    λ[:,i] = sort(eigvals(J, M), by=LinearAlgebra.eigsortby)

    # update initial guess for the state variables
    u0 .= u
end</code></pre><p>We now plot the results predicted using each aerodynamic model.</p><pre><code class="language-">using Plots
pyplot()

default(
    titlefontsize = 14,
    legendfontsize = 11,
    guidefontsize = 14,
    tickfontsize = 11,
    foreground_color_legend = nothing,
    background_color_legend = nothing,
    minorgrid=true,
    framestyle = :zerolines)

sp1 = plot(
    xlim = (1, 30),
    xtick = vcat(1, 5:5:30),
    xlabel = &quot;Velocity (m/s)&quot;,
    ylim = (0, 90),
    ytick = 0.0:10:90,
    ylabel = &quot;Frequency (rad/s)&quot;,
    legend = :topright
    )

sp2 = plot(
    xlim = (1, 30),
    xtick = vcat(1, 5:5:30),
    xlabel = &quot;Velocity (m/s)&quot;,
    ylim = (-12, 8),
    ytick = -12:4:8,
    ylabel = &quot;Damping Ratio %&quot;,
    legend = :topleft
    )

for i = 1:size(λ, 1)

    idx = findall(x -&gt; abs(x) &lt; 500, λ[i,:])
    Vi = Vinf[idx]
    λi = λ[i,idx]

    scatter!(sp1, Vi, imag.(λi),
        label = &quot;&quot;,
        color = 1,
        markersize = 3,
        markerstrokewidth = 0,
        )
end

for i = 1:size(λ, 1)

    idx = findall(x -&gt; abs(x) &lt; 500, λ[i,:])
    Vi = Vinf[idx]
    λi = λ[i,idx]

    scatter!(sp2, Vi,
        # real.(λi),
        real.(λi)./abs.(λi)*100,
        label = &quot;&quot;,
        color = 1,
        markersize = 3,
        markerstrokewidth = 0,
        )
end

p1 = plot(sp1, sp2, layout = (2, 1), size = (600, 800), show=true)</code></pre></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../">« Home</a><a class="docs-footer-nextpage" href="../theory/">Theory »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> on <span class="colophon-date" title="Friday 18 June 2021 01:16">Friday 18 June 2021</span>. Using Julia version 1.6.1.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
