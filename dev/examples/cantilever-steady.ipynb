{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Steady State Aeroelastic Analysis of a Highly Flexible Wing\n",
    "\n",
    "In this example, we demonstrate how to find steady state operating conditions for a\n",
    "highly flexible cantilever wing\n",
    "\n",
    "![](../assets/cantilever-wing.png)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "The wing we are considering in this example was created by modifying Daedalus aircraft\n",
    "data and is therefore representative of a high-altitude long-endurance wing. It has a\n",
    "16 meter span (from root to tip) and a 1 meter chord. To model the wing's aerodynamics,\n",
    "we use a lifting line model. To model the wing's structure, we use a geometrically exact\n",
    "beam theory model."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Aeroelasticity, GXBeam, NLsolve, LineSearches, LinearAlgebra\n",
    "\n",
    "# --- Initial Setup --- #\n",
    "\n",
    "# discretization\n",
    "N = 8 # number of elements\n",
    "\n",
    "# geometric properties\n",
    "span = 16 # m\n",
    "chord = 1 # m\n",
    "xref = 0.5 # normalized reference location (from leading edge)\n",
    "xcg = 0.5 # center of gravity (from leading edge)\n",
    "\n",
    "# stiffness properties\n",
    "GJ = 1e4 # N*m^2 (torsional rigidity)\n",
    "EIyy = 2e4 # N*m^2 (flat bending rigidity)\n",
    "EIzz = 4e6 # N*m^2 (chord bending rigidity)\n",
    "\n",
    "# inertial properties\n",
    "mu = 0.75 # kg/m (mass per unit length)\n",
    "i11 = 0.1 # kg*m (moment of inertia about elastic axis)\n",
    "i22 = 0.0375 # moment of inertia about beam y-axis\n",
    "i33 = 0.0625 # moment of inertia about beam z-axis\n",
    "\n",
    "# freestream properties\n",
    "Vinf = 25.0 # m/s (velocity)\n",
    "rho = 0.0889 # kg/m^3 (air density)\n",
    "alpha = 10*pi/180 # angle of attack\n",
    "c = 343.0 # m/s (air speed of sound)\n",
    "beta = sqrt(1 - Vinf^2/c^2) # Prandtl-Glauert compressibility correction factor\n",
    "\n",
    "# aerodynamic section properties\n",
    "a = xref - 0.5 # normalized reference location (relative to semi-chord)\n",
    "b = chord / 2 # m (semi-chord)\n",
    "a0 = 2*pi # lift slope (for each section)\n",
    "alpha0 = 0.0 # zero lift angle of attack (for each section)\n",
    "cd0 = 0.01\n",
    "cm0 = 0\n",
    "\n",
    "# define geometry (assume NED coordinate frame)\n",
    "xpt = range(0, 0, length=N+1) # point x-coordinates (in body frame)\n",
    "ypt = range(0, span, length=N+1) # point y-coordinates (in body frame)\n",
    "zpt = range(0, 0, length=N+1) # point z-coordinates (in body frame)\n",
    "points = [[xpt[i],ypt[i],zpt[i]] for i = 1:N+1]\n",
    "start = 1:N # starting point of each beam element\n",
    "stop = 2:N+1 # ending point of each beam element\n",
    "e1 = [0, 1,  0] # beam x-axis\n",
    "e2 = [1, 0,  0] # beam y-axis\n",
    "e3 = [0, 0, -1] # beam z-axis\n",
    "frames = fill([e1 e2 e3], N) # local to body frame transformation\n",
    "compliance = fill(Diagonal([0, 0, 0, 1/GJ, 1/EIyy, 1/EIzz]), N) # compliance matrix\n",
    "mass = fill(Diagonal([mu, mu, mu, i11, i22, i33]), N) # mass matrix\n",
    "assembly = GXBeam.Assembly(points, start, stop; frames, compliance, mass)\n",
    "\n",
    "prescribed_conditions = Dict(\n",
    "    # fixed left edge\n",
    "    1 => GXBeam.PrescribedConditions(ux=0, uy=0, uz=0, theta_x=0, theta_y=0, theta_z=0),\n",
    ")\n",
    "\n",
    "# define GXBeam system\n",
    "system = DynamicSystem(assembly)\n",
    "\n",
    "# --- Define Submodels --- #\n",
    "\n",
    "# construct section models (we use Peters' finite state model in this case)\n",
    "section_models = fill(Peters{6}(), N)\n",
    "\n",
    "# construct aerodynamic model\n",
    "aerodynamic_model = LiftingLine(section_models)\n",
    "\n",
    "# construct structural model\n",
    "structural_model = GXBeamAssembly(system; structural_damping=false)\n",
    "\n",
    "# define submodels\n",
    "submodels = (aerodynamic_model, structural_model)\n",
    "\n",
    "# --- Define Initial Parameters --- #\n",
    "\n",
    "V = [-Vinf*cos(alpha), 0.0, -Vinf*sin(alpha)] # m/s (freestream velocity)\n",
    "\n",
    "# define parameters for each lifting line section\n",
    "section_parameters = fill([a, b, a0, alpha0, cd0, cm0], N)\n",
    "\n",
    "# define parameters for the lifting line model\n",
    "liftingline_parameters = LiftingLineParameters(section_parameters)\n",
    "\n",
    "# define parameters for the geometrically exact beam theory model\n",
    "gxbeam_parameters = GXBeamParameters(assembly)\n",
    "\n",
    "# define parameters for the coupling\n",
    "coupling_parameters = LiftingLineGXBeamParameters(V, rho, beta;\n",
    "    prescribed_conditions = prescribed_conditions,\n",
    "    gravity = [0, 0, 9.81])\n",
    "\n",
    "# combine parameters\n",
    "parameters = (liftingline_parameters, gxbeam_parameters, coupling_parameters)\n",
    "\n",
    "# --- Define Coupled Model --- #\n",
    "\n",
    "model = CoupledModel(submodels, parameters; symbolic=false)\n",
    "\n",
    "# --- Perform Analysis --- #\n",
    "\n",
    "# state rates equal to zero\n",
    "dx = zeros(number_of_states(model))\n",
    "\n",
    "# use previously defined parameters\n",
    "p = parameters\n",
    "\n",
    "# set time to zero\n",
    "t = 0.0\n",
    "\n",
    "# define residual function\n",
    "f! = (resid, x) -> residual!(resid, model, dx, x, p, t)\n",
    "\n",
    "# define jacobian function\n",
    "j! = (jacob, x) -> state_jacobian!(jacob, model, dx, x, p, t)\n",
    "\n",
    "# define initial guess\n",
    "x0 = zeros(number_of_states(model))\n",
    "\n",
    "# find steady state operating conditions\n",
    "result = nlsolve(f!, j!, x0; method=:newton, linesearch=LineSearches.BackTracking())\n",
    "\n",
    "# separate state variables into aerodynamic and structural states\n",
    "Î», x = separate_states(result.zero, model)\n",
    "\n",
    "# post-process the structural state\n",
    "state = AssemblyState(x, system, assembly; prescribed_conditions)\n",
    "\n",
    "# --- Visualize the Result --- #\n",
    "\n",
    "# DAE-11 Airfoil\n",
    "dae_11 = [\n",
    "    1.0         0.0\n",
    "    0.986283    0.0010768\n",
    "    0.969651    0.0023497\n",
    "    0.947994    0.0038679\n",
    "    0.920015    0.0056143\n",
    "    0.888329    0.007331\n",
    "    0.856562    0.0087586\n",
    "    0.824998    0.0099002\n",
    "    0.793421    0.0107738\n",
    "    0.761789    0.0113933\n",
    "    0.730101    0.0117728\n",
    "    0.698376    0.0119312\n",
    "    0.666635    0.0118657\n",
    "    0.634882    0.0115701\n",
    "    0.603112    0.0110455\n",
    "    0.571312    0.0102848\n",
    "    0.539469    0.0093041\n",
    "    0.507575    0.0081154\n",
    "    0.475605    0.0067337\n",
    "    0.443554    0.0052\n",
    "    0.411434    0.0035362\n",
    "    0.37924     0.0017525\n",
    "    0.346965   -0.0001193\n",
    "    0.314611   -0.0020681\n",
    "    0.282172   -0.0040608\n",
    "    0.249739   -0.0060686\n",
    "    0.21745    -0.0080524\n",
    "    0.185384   -0.0099672\n",
    "    0.153678   -0.011747\n",
    "    0.122584   -0.0132928\n",
    "    0.0926077  -0.0144625\n",
    "    0.0648951  -0.0150664\n",
    "    0.0414512  -0.0148893\n",
    "    0.0245731  -0.0138784\n",
    "    0.0141149  -0.0123747\n",
    "    0.0080862  -0.0107002\n",
    "    0.0044148  -0.0087908\n",
    "    0.0020116  -0.0064284\n",
    "    0.0004919  -0.003486\n",
    "    0.0         0.0\n",
    "    0.000757    0.0051431\n",
    "    0.0032916   0.0114678\n",
    "    0.0082926   0.0193346\n",
    "    0.016496    0.0287274\n",
    "    0.028364    0.0393002\n",
    "    0.0439665   0.0505399\n",
    "    0.0628794   0.0618045\n",
    "    0.0844076   0.0725648\n",
    "    0.107915    0.082517\n",
    "    0.132934    0.091527\n",
    "    0.159095    0.0995398\n",
    "    0.186118    0.106551\n",
    "    0.213791    0.112558\n",
    "    0.24194     0.117585\n",
    "    0.270425    0.121658\n",
    "    0.299149    0.124802\n",
    "    0.328056    0.127043\n",
    "    0.357092    0.128382\n",
    "    0.386183    0.12885\n",
    "    0.415255    0.128434\n",
    "    0.444274    0.127109\n",
    "    0.473223    0.124888\n",
    "    0.502047    0.121778\n",
    "    0.530681    0.117774\n",
    "    0.559141    0.112838\n",
    "    0.587581    0.106907\n",
    "    0.616266    0.0999911\n",
    "    0.645437    0.0922048\n",
    "    0.675223    0.0837204\n",
    "    0.705653    0.0747378\n",
    "    0.736604    0.0655023\n",
    "    0.767351    0.0564167\n",
    "    0.79778     0.0476052\n",
    "    0.82795     0.0391518\n",
    "    0.857951    0.0311284\n",
    "    0.887967    0.0235641\n",
    "    0.918609    0.0163798\n",
    "    0.947875    0.0100446\n",
    "    0.970008    0.0056151\n",
    "    0.986483    0.0025379\n",
    "    1.0        -0.0\n",
    "]\n",
    "\n",
    "sections = zeros(3, size(dae_11, 1), length(points))\n",
    "sections[1,:,:] .= (xref .- dae_11[:,1]) * chord\n",
    "sections[3,:,:] .= (-dae_11[:,2]) * chord\n",
    "\n",
    "# visualize the result\n",
    "write_vtk(\"cantilever-steady-undeformed\", assembly; sections=sections)\n",
    "write_vtk(\"cantilever-steady-deformed\", assembly, state; sections=sections)"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.1"
  },
  "kernelspec": {
   "name": "julia-1.9",
   "display_name": "Julia 1.9.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
